#
# Copyright 2007-2015, Kaazing Corporation. All rights reserved.
#
# Licensed under the Apache License, Version 2.0 (the "License");
# you may not use this file except in compliance with the License.
# You may obtain a copy of the License at
#
# http://www.apache.org/licenses/LICENSE-2.0
#
# Unless required by applicable law or agreed to in writing, software
# distributed under the License is distributed on an "AS IS" BASIS,
# WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
# See the License for the specific language governing permissions and
# limitations under the License.
#

connect http://localhost:8001/echo
connected

# Connection request
write method "GET"
write version "HTTP/1.1"
write header host
write header "Upgrade" "websocket"
write header "Connection" "Upgrade"
write header "Sec-WebSocket-Key" "N+HvJLWWWO2lG89OebjRMg=="
write header "Sec-WebSocket-Version" "13"
write header "Sec-WebSocket-Protocol" "x-kaazing-handshake"

# 101 Response
read status "101" /.+/
read version "HTTP/1.1"
read header "Upgrade" "websocket"
read header "Connection" "Upgrade"
read header "Sec-WebSocket-Accept" /.+/
read header "Sec-WebSocket-Protocol" "x-kaazing-handshake"
read header "Server" /.+/
read header "Date" /.+/

# Extended handshake request
write [0x82 0xFE 0x00 0xB0] [0x00 0x00 0x00 0x00]
write "GET /echo?token=joe HTTP/1.1\r\n"
write "WebSocket-Protocol: \r\n"
write "Sec-WebSocket-Protocol: \r\n"
write "Sec-WebSocket-Extensions: x-kaazing-idle-timeout,x-kaazing-ping-pong,x-kaazing-http-revalidate\r\n"
write "\r\n"

# Extended handshake response
read [0x82 0x7E]

read notify JOE_WSN_SESSION_ESTABLISHED

write await CLOSE_SESSIONS_INVOKED

# write something to keep the session from closing
# if I remove the write then the script hangs after the await barrier and the test timeouts
# please see k3po/k3po issue #237 filed for the above reason
# if I don't have the barrier then the session will close(probably because it has no further instructions)
# and the test becomes invalid(as I am trying to use closeSessions() on an already closed session)
write "something"
