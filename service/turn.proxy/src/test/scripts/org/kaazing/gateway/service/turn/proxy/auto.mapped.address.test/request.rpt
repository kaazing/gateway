#
# Copyright 2007-2016, Kaazing Corporation. All rights reserved.
#
# Licensed under the Apache License, Version 2.0 (the "License");
# you may not use this file except in compliance with the License.
# You may obtain a copy of the License at
#
# http://www.apache.org/licenses/LICENSE-2.0
#
# Unless required by applicable law or agreed to in writing, software
# distributed under the License is distributed on an "AS IS" BASIS,
# WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
# See the License for the specific language governing permissions and
# limitations under the License.
#

property connectURI "tcp://localhost:3478"
property peerAddressClient "192.0.2.1"
property messageIntegrityClient "joe:example.com:welcome"

connect ${connectURI}
connected

##Allocation request
#STUN Message type | Message Length (***CONFIRM***)
write [0x00] [0x03] [0x00] [0x14]
#Magic Cookie (fixed required value 0x2112A442)
write [0x21] [0x12] [0xa4] [0x42]
#Transaction ID
write ${turn:generateTransactionId()}
#Attribute Lifetime
write [0x00] [0x0d] [0x00] [0x04]
write [0x00] [0x00] [0x0e] [0x10] #3600 seconds
#Attribute Requested Transport (Type|Length|Value)
write [0x00] [0x19] [0x00] [0x01]
write [0x11] [0x00] [0x00] [0x00] #UDP is 17
#Attribute Don't Fragment (Type|Length|Value)
write [0x00] [0x1a] [0x00] [0x00]

#401 Allocation error response
read [0x01] [0x13] [0x00] [0x48]
read [0x21] [0x12] [0xa4] [0x42]
read ([0..12]:transactionID)
#Attributes
read [0x00] [0x09] [0x00] [0x10]
read [0x00] [0x00] [0x04] [0x01]
read "Unauthorized"
#Attributes
read [0x00] [0x14] [0x00] [0x0b]
read ([0..11]:realm) [0x00]
#Attributes
read [0x00] [0x15] [0x00] [0x20]
read ([0..32]:nonce)

##Allocation request (w/ cred)
#STUN Message type | Message Length (***CONFIRM***)
write [0x00] [0x03] [0x00] [0x68]
#Magic Cookie
write [0x21] [0x12] [0xa4] [0x42]
#Transaction ID
write ${turn:generateTransactionId()}
#Attribute Lifetime
write [0x00] [0x0d] [0x00] [0x04]
write [0x00] [0x00] [0x0e] [0x10] #3600 seconds
#Attribute Requested Transport
write [0x00] [0x19] [0x00] [0x01]
write [0x11] [0x00] [0x00] [0x00]
#Attribute Don't Fragment
write [0x00] [0x1a] [0x00] [0x00]
#Attribute Username
write [0x00] [0x06] [0x00] [0x03]
write "joe" [0x00]
#Attribute Realm
write [0x00] [0x14] [0x00] [0x0b]
write ${realm} [0x00]
#Attribute Nonce
write [0x00] [0x15] [0x00] [0x20]
write ${nonce}
#Attribute Message-Integrity
write [0x00] [0x08] [0x00] [0x14]
write [0x01] [0x02] [0x03] [0x04] ${turn:messageDigestMD5Encoding("joe:example.com:welcome")}

#200 response w/ mapped address
#STUN Header
read [0x01] [0x03] [0x00] [0x40]
read [0x21] [0x12] [0xa4] [0x42]
read ${transactionID}
#Attribute Lifetime
read [0x00] [0x0d] [0x00] [0x04]
read [0x00] [0x00] [0x0c] [0x08]
#Attribute XOR-Relayed-Address
read [0x00] [0x16] [0x00] [0x08]
read [0x00] [0x01]
read [0..2]
read ([0..4]:relay_address)
#Attribute XOR-Mapped-Address
read [0x00] [0x20] [0x00] [0x08]
read [0x00] [0x01]
read [0..2]
read [0..4]
#Attribute Message-Integrity
read [0x00] [0x08] [0x00] (byte:length)
read ([0..${length}]:messageDigest)
#Attribute Fingerprint
read [0x80] [0x28] [0x00] [0x04]
read ([0..4]:fingerprint)

#creates permissions for peer
#STUN Header
write [0x00] [0x08] [0x00] [0x5c] #Type|Length
write [0x21] [0x12] [0xa4] [0x42] #Magic Cookie
write ${transactionID}
#Attribute XOR-Peer-Address
write [0x00] [0x12] [0x00] [0x08]

# unmodified peer address
write [0x00] [0x01] ${turn:portXOR(8001)} #port 8001
write ${turn:ipXOR("192.0.2.16")}

#Attribute Username
write [0x00] [0x06] [0x00] [0x03]
write "joe" [0x00]
#Attribute Realm
write [0x00] [0x14] [0x00] [0x0b]
write ${realm} [0x00]
#Attribute Nonce
write [0x00] [0x15] [0x00] [0x20]
write ${nonce}
#Attribute Message-Integrity
write [0x00] [0x08] [0x00] ${length}
write [0x01] [0x02] [0x03] [0x04] ${turn:messageDigestMD5Encoding("joe:example.com:welcome")}

#success
read [0x01] [0x08] [0x00] [0x18]
read [0x21] [0x12] [0xa4] [0x42]
read ${transactionID}
#attribute Message-Integrity
read [0x00] [0x08] [0x00] ${length}
read ${messageDigest}

#binds to peer
#STUN Header
write [0x00] [0x09] [0x00] [0x68] #Type|Length
write [0x21] [0x12] [0xa4] [0x42] #Magic Cookie
write ${transactionID}
#Attribute Channel-Number
write [0x00] [0x0c] [0x00] [0x02]
write [0x40] [0x00] [0x00] [0x00]
#Attribute XOR-Peer-Address
write [0x00] [0x12] [0x00] [0x08]
write [0xff] [0x01] ${turn:portXOR(8001)} #port 8001
write ${turn:ipXOR("192.0.2.1")}
#Attribute Username
write [0x00] [0x06] [0x00] [0x03]
write "joe" [0x00]
#Attribute Realm
write [0x00] [0x14] [0x00] [0x0b]
write ${realm} [0x00]
#Attribute Nonce
write [0x00] [0x15] [0x00] [0x20]
write ${nonce}
#Attribute Message-Integrity
write [0x00] [0x08] [0x00] [0x14]
write [0x01] [0x02] [0x03] [0x04] ${turn:messageDigestMD5Encoding("joe:example.com:welcome")}

#success
read [0x01] [0x09] [0x00] [0x18]
read [0x21] [0x12] [0xa4] [0x42]
read ${transactionID}
#attribute Message-Integrity
read [0x00] [0x08] [0x00] ${length}
read ${messageDigest}
