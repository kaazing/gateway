#
# Copyright 2007-2016, Kaazing Corporation. All rights reserved.
#
# Licensed under the Apache License, Version 2.0 (the "License");
# you may not use this file except in compliance with the License.
# You may obtain a copy of the License at
#
# http://www.apache.org/licenses/LICENSE-2.0
#
# Unless required by applicable law or agreed to in writing, software
# distributed under the License is distributed on an "AS IS" BASIS,
# WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
# See the License for the specific language governing permissions and
# limitations under the License.
#

##################################################################################################
## This script reflects Kaazing 4.0 JavaScript client running in IE11 doing emulated WebSocket
##################################################################################################
## Scenario is: connect with Basic auth (extended handshake), send a message, expect to receive the text "OK"

# Conversation 1: create and upstream
# -----------------------------------

connect tcp://127.0.0.1:8001
connected

write "GET /basic/;e/ctm?.kn=1902872333548104 HTTP/1.1\r\n"
write "Accept: */*\r\n"
write "Content-Type: text/plain; charset=utf-8\r\n"
write "X-WebSocket-Version: wseb-1.0\r\n"
write "X-Accept-Commands: ping\r\n"
write "X-WebSocket-Protocol: x-kaazing-bump\r\n"
write "X-Origin: http://localhost:8001\r\n"
write "Referer: http://localhost:8001/?.kr=xs\r\n"
write "Accept-Language: en-US\r\n"
write "Accept-Encoding: gzip, deflate\r\n"
write "User-Agent: Mozilla/5.0 (Windows NT 6.1; WOW64; Trident/7.0; rv:11.0) like Gecko\r\n"
write "Host: localhost:8001\r\n"
write "DNT: 1\r\n"
write "Connection: Keep-Alive\r\n"
write "\r\n"

# HTTP response (challenge) - flat since we're Basic not Application Basic
read "HTTP/1.1 401 Unauthorized\r\n"
read "Content-Type: text/html\r\n"
read "WWW-Authenticate: Basic realm=\"Kaazing WebSocket Gateway Demo\"\r\n"
read "Content-Length: 64\r\n"
read "\r\n"
read "<html><head></head><body><h1>401 Unauthorized</h1></body></html>"

write "GET /basic/;e/ctm?.kn=1902872333548104 HTTP/1.1\r\n"
write "Accept: */*\r\n"
write "Content-Type: text/plain; charset=utf-8\r\n"
write "X-WebSocket-Version: wseb-1.0\r\n"
write "X-Accept-Commands: ping\r\n"
write "X-WebSocket-Protocol: x-kaazing-bump\r\n"
write "X-Origin: http://localhost:8001\r\n"
write "Referer: http://localhost:8001/?.kr=xs\r\n"
write "Accept-Language: en-US\r\n"
write "Accept-Encoding: gzip, deflate\r\n"
write "User-Agent: Mozilla/5.0 (Windows NT 6.1; WOW64; Trident/7.0; rv:11.0) like Gecko\r\n"
write "Host: localhost:8001\r\n"
write "DNT: 1\r\n"
write "Connection: Keep-Alive\r\n"
write "Authorization: Basic am9lOndlbGNvbWU=\r\n"
write "\r\n"

# Enveloped HTTP response
read "HTTP/1.1 200 OK\r\n"
read "Cache-Control: no-cache\r\n"
read "Content-Length: 200\r\n"
read "Content-Type: text/plain;charset=UTF-8\r\n"
# Example: read "Date: Fri, 19 Dec 2014 05:01:28 GMT\r\n"
read /Date: .*/ "\r\n"
read "Server: Kaazing Gateway\r\n"
read "\r\n"
read "HTTP/1.1 201 Created\r\n"
read "Content-Type: text/plain;charset=UTF-8\r\n"
read "\r\n"
# Example:
# read "http://localhost:8001/basic/;e/utm/ZwoVZ7tzQE9MJxRlOhYRmaFTsV12wzbu\n"
# read "http://localhost:8001/basic/;e/dtm/ZwoVZ7tzQE9MJxRlOhYRmaFTsV12wzbu\n"
read "http://localhost:8001/basic/;e/utm/"
read [(:sessionId){32}] 
read "\n"
read "http://localhost:8001/basic/;e/dtm/"
read ${sessionId} 
read "\n"

write "POST /basic/;e/utm/"
write ${sessionId}
write " HTTP/1.1\r\n"
write "Accept: */*\r\n"
write "Content-Type: text/plain; charset=utf-8\r\n"
write "X-Origin: http://localhost:8001\r\n"
write "Referer: http://localhost:8001/?.kr=xs\r\n"
write "Accept-Language: en-US\r\n"
write "Accept-Encoding: gzip, deflate\r\n"
write "User-Agent: Mozilla/5.0 (Windows NT 6.1; WOW64; Trident/7.0; rv:11.0) like Gecko\r\n"
write "Host: localhost:8001\r\n"
write "Content-Length: 10\r\n"
write "DNT: 1\r\n"
write "Connection: Keep-Alive\r\n"
write "Cache-Control: no-cache\r\n"
write "Authorization: Basic am9lOndlbGNvbWU=\r\n"
write "\r\n"

# Message "Hi"
write [0xc2 0x80 0x02]
write "Hi"

# reconnect
write [0x01 0x30 0x31 0xc3 0xbf]

read "HTTP/1.1 200 OK\r\n"
read "Content-Length: 19\r\n"
read "Content-Type: text/plain;charset=UTF-8\r\n"
read /Date: .*/ "\r\n"
read "Server: Kaazing Gateway\r\n"
read "\r\n"
read "HTTP/1.1 200 OK\r\n"
read "\r\n"

read notify CREATED

write await RESPONSE_RECEIVED

# Do a clean close according to the wseb spec
write "POST /basic/;e/utm/"
write ${sessionId}
write " HTTP/1.1\r\n"
write "Accept: */*\r\n"
write "Content-Type: text/plain; charset=utf-8\r\n"
write "X-Origin: http://localhost:8001\r\n"
write "Referer: http://localhost:8001/?.kr=xs\r\n"
write "Accept-Language: en-US\r\n"
write "Accept-Encoding: gzip, deflate\r\n"
write "User-Agent: Mozilla/5.0 (Windows NT 6.1; WOW64; Trident/7.0; rv:11.0) like Gecko\r\n"
write "Host: localhost:8001\r\n"
write "Content-Length: 10\r\n"
write "DNT: 1\r\n"
write "Connection: Keep-Alive\r\n"
write "Cache-Control: no-cache\r\n"
write "Authorization: Basic am9lOndlbGNvbWU=\r\n"
write "\r\n"

# Request WS Connection Close
write [0x01 0x30 0x32 0xc3 0xbf]

# always end with reconnect frame per wseb spec
write [0x01 0x30 0x31 0xc3 0xbf]

read "HTTP/1.1 200 OK\r\n"
read "Content-Length: 19\r\n"
read "Content-Type: text/plain;charset=UTF-8\r\n"
read /Date: .*/ "\r\n"
read "Server: Kaazing Gateway\r\n"
read "\r\n"
read "HTTP/1.1 200 OK\r\n"
read "\r\n"

close
closed

# Conversation 2: downstream
# --------------------------

connect tcp://127.0.0.1:8001
connected

write await CREATED

write "GET /basic/;e/dtm/"
write ${sessionId}
write "?.kc=text/plain;charset=windows-1252&.kb=4096&.kid=891691231737601 HTTP/1.1\r\n"
write "Accept: */*\r\n"
write "X-Origin: http://localhost:8001\r\n"
write "Referer: http://localhost:8001/?.kr=xs\r\n"
write "Accept-Language: en-US\r\n"
write "Accept-Encoding: gzip, deflate\r\n"
write "User-Agent: Mozilla/5.0 (Windows NT 6.1; WOW64; Trident/7.0; rv:11.0) like Gecko\r\n"
write "Host: localhost:8001\r\n"
write "DNT: 1\r\n"
write "Connection: Keep-Alive\r\n"
write "Authorization: Basic am9lOndlbGNvbWU=\r\n"
write "\r\n"

read "HTTP/1.1 200 OK\r\n"
read "Cache-Control: private\r\n"
read "Connection: close\r\n"
read "Content-Type: text/plain;charset=windows-1252\r\n"
read /Date:.*/ "\r\n"
read "Server: Kaazing Gateway\r\n"
read "X-Content-Type-Options: nosniff\r\n"
read "\r\n"
read "HTTP/1.1 200 OK\r\n"
read "Content-Type: text/plain;charset=windows-1252\r\n"
read "X-Idle-Timeout: 60\r\n"
read "\r\n"

read [0x01 0x30 0x30 0xff]

# Expect response "OK" indicating the subject was found on the WsnSession
read [0x80 0x02]
read "OK"

# Padding
read [0x01]
# N occurrences of 0x30
read [0..2040]
read [0xff]

read notify RESPONSE_RECEIVED

# close command
read [0x01 0x30 0x32 0xff]
# reconnect command
read [0x01 0x30 0x31 0xff]

closed